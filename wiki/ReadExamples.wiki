= Read Examples =

Mongrel has the following functions for reading documents:
 * `count/1`
 * `count/2`
 * `find/1`
 * `find/2`
 * `find/3`
 * `find/4`
 * `find_one/1`
 * `find_one/2`
 * `find_one/3`
 
==Basic Finds==
To find a single author, we can use the `find_one/1` function:

<pre>
10> {ok, Conn}=mongo:connect(localhost).
{ok,{connection,{"localhost",27017},<0.64.0>,infinity}}
11> mongrel:do(safe,master,Conn,mongrel_books,fun()->
11> mongrel:find_one(#author{})                      
11> end).                                            
{ok,#author{'_id' = {<<79,138,150,247,138,250,150,23,62,
                       0,0,1>>},
            first_name = undefined,last_name = <<"Eliott">>}}
12> 
</pre>

To get all authors, we can use the `find/1` function that returns a `mongrel_cursor`. The `mongrel_cursor:rest/1` function returns all the results from the cursor. See the MongrelCursors page for some details of what you should know about Mongrel cursors; the most important point is that you *cannot* use a Mongrel cursor outside of the scope of the `mongrel:do/5` function. 

<pre>
12> f().                                             
ok
13> {ok, Conn}=mongo:connect(localhost).             
{ok,{connection,{"localhost",27017},<0.69.0>,infinity}}
14> mongrel:do(safe,master,Conn,mongrel_books,fun()->
14> Cursor=mongrel:find(#author{}),                  
14> mongrel_cursor:rest(Cursor)
14> end).
{ok,[#author{'_id' = {<<79,138,150,247,138,250,150,23,62,
                        0,0,1>>},
             first_name = undefined,last_name = <<"Eliott">>},
     #author{'_id' = {<<79,138,150,247,138,250,150,23,62,0,0,4>>},
             first_name = <<"Edmund">>,last_name = <<"Wells">>},
     #author{'_id' = {<<79,138,150,247,138,250,150,23,62,0,0,7>>},
             first_name = <<"Charles">>,last_name = <<"Dikkens">>}]}
15> 
</pre>

==Counting Documents==
The example code below shows how to count all the books written by Edmund Wells.

<pre>
10> f().
ok 
11> {ok,Conn}=mongo:connect(localhost).
{ok,{connection,{"localhost",27017},<0.68.0>,infinity}}
12> mongrel:do(safe, master, Conn, mongrel_books, fun() ->  
12> EdmundWells = mongrel:find_one(#author{first_name= <<"Edmund">>, last_name= <<"Wells">>}),
12> mongrel:count(#book{author=EdmundWells})  
12> end).       
{ok,2}
</pre>

Using the Mongrel API to find all the books not written by Edmund Wells is more complicated, and we have to use a '$ne' QueryConditional

<pre>
10> mongrel:do(safe, master, Conn, mongrel_books, fun() -> 
10> EdmundWells = mongrel:find_one(#author{first_name= <<"Edmund">>, last_name= <<"Wells">>}),
10> mongrel:count(#book{author=#author{'_id'={'$ne', EdmundWells#author.'_id'}}})             
10> end).
{ok,4}    
</pre>

That query may be seen as so complicated that it may be simpler use the `mongo` count/2 function instead. Contrast the above code with the code below.

<pre>
11> mongrel:do(safe, master, Conn, mongrel_books, fun() -> 
11> EdmundWells=mongrel:find_one(#author{first_name= <<"Edmund">>, last_name= <<"Wells">>}),
11> mongo:count(book, {'author.#id', {'$ne', EdmundWells#author.'_id'}})
11> end).   
{ok,4}
</pre>

==Finding Projections of Documents==
Suppose that we want to find all book titles that don't have an upper- or lower-case 'H' anywhere in the title. The query below shows a suitable query. The second argument to the find/2 function gets a projection of only the book titles. We write a somewhat clumsy expression that uses both a regular expression and a QueryConditional ('$not') for illustrative purposes.

<pre>
18> {ok, Books}=mongrel:do(safe, master, Conn, mongrel_books, fun() ->          
18> Cursor=mongrel:find(#book{title= {'$not', {regex, <<"h">>, <<"i">>}}}, #book{title=1}),     
18> mongrel_cursor:rest(Cursor)
18> end).
{ok,[#book{'_id' = {<<79,137,174,200,138,250,150,14,249,
                      0,0,14>>},
           title = <<"David Copperfield">>,isbn = undefined,
           author = undefined,reviews = undefined},
     #book{'_id' = {<<79,137,174,200,138,250,150,14,249,0,0,15>>},
           title = <<"Grate Expectations">>,isbn = undefined,
           author = undefined,reviews = undefined},
     #book{'_id' = {<<79,137,174,200,138,250,150,14,249,0,0,17>>},
           title = <<"Rarnaby Budge">>,isbn = undefined,
           author = undefined,reviews = undefined}]}
19> 
</pre>

==Advanced Queries==
MongoDB allows options to be passed with a query such as 'hint' of an index to use when executing the query or that the results must be ordered in some way. We issue queries with options using a tuple of key-value pairs where one key must be '$query' and the corresponding value is a record that we want to use when selecting results.

If we want to issue a query where we order our results we need to supply a document to the find function with '$query' and '$orderby' keys. For example, the code below finds all books with a "T" somewhere in the title and returns them in alphabetical order

<pre>
21> f().
ok                                                
22> {ok,Conn}=mongo:connect(localhost).    
{ok,{connection,{"localhost",27017},<0.85.0>,infinity}}
23> {ok, Books}=mongrel:do(safe, master, Conn, mongrel_books, fun() -> 
23> Cursor=mongrel:find({'$orderby', #book{title=1}, '$query', #book{title= {regex, <<"t">>, <<"i">>}}}, #book{title=1, '_id'=0}),
23> mongrel_cursor:rest(Cursor)                                                 
23> end).
{ok,[#book{'_id' = undefined,                     
           title = <<"A Hundred and One Ways to start a Fight">>,
           isbn = undefined,author = undefined,reviews = undefined},
     #book{'_id' = undefined,title = <<"Grate Expectations">>,
           isbn = undefined,author = undefined,reviews = undefined},
     #book{'_id' = undefined,
           title = <<"Olsen's Standard Book of British Birds">>,
           isbn = undefined,author = undefined,reviews = undefined},
     #book{'_id' = undefined,
           title = <<"Thirty Days in the Samarkind Desert with the Duchess of Kent">>,
           isbn = undefined,author = undefined,reviews = undefined}]}
24> 

</pre>